prototype(Tms.Consent:Component.Atom.NoConsentPlaceholder) < prototype(Neos.Fusion:Component) {
    @if.consentEnabled = ${Configuration.setting('Tms.Consent.enabled')}
    @if.appConfiguredAndEnabled = ${this.appName && Configuration.setting('Tms.Consent.config.apps.' + this.appName + '.enabled')}

    appName = ''
    imageUri = ''

    i18nContentBlocked = ${Translation.translate('noConsentPlaceholder.contentBlocked', 'content blocked', [], null, 'Tms.Consent')}
    i18nReviewSettings = ${Translation.translate('noConsentPlaceholder.reviewSettings', 'review your consent settings', [], null, 'Tms.Consent')}

    renderer = afx`
        <div class="tms-consent_no-consent-placeholder" data-name={props.appName}>
            <p>
                <span data-klaro-app-title={props.appName}>{props.appName}</span> {props.i18nContentBlocked} - <a href="#" onclick="return klaro.show();">{props.i18nReviewSettings}</a>
            </p>
            <img @if.hasImage={props.imageUri} src={props.imageUri} alt="" loading="lazy" />
        </div>
    `
}
