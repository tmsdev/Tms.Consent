prototype(Tms.Consent:Component.Atom.NoConsentPlaceholder) < prototype(Neos.Fusion:Component) {
    @if.consentEnabled = ${Configuration.setting('Tms.Consent.enabled')}
    @if.serviceConfiguredAndEnabled = ${this.serviceName && Configuration.setting('Tms.Consent.config.services.' + this.serviceName + '.enabled')}

    serviceName = ''
    imageUri = ''
    imageAlternativeText = ''

    i18nContentBlocked = ${Translation.translate('noConsentPlaceholder.contentBlocked', 'content blocked', [], null, 'Tms.Consent')}
    i18nConsentButton = ${Translation.translate('noConsentPlaceholder.consentButton', 'Show this content', [], null, 'Tms.Consent')}

    renderer = afx`
        <div class="tms-consent_no-consent-placeholder" data-name={props.serviceName}>
            <p>
                <span data-klaro-service-title={props.serviceName}>{props.serviceName}</span> {props.i18nContentBlocked}
            </p>
            <button data-consent-button="true" data-name={props.serviceName}>{props.i18nConsentButton}</button>
            <div @if.hasImage={props.imageUri}>
                <img src={props.imageUri} alt={props.imageAlternativeText} loading="lazy" />
            </div>
        </div>
    `
}
