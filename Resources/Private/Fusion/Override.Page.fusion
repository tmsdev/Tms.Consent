prototype(Neos.Neos:Page) {
    head.stylesheets {
        tmsConsent = Neos.Fusion:Tag {
            @if.enabled = ${Configuration.setting('Tms.Consent.enabled')}
            @if.autoIncludeStyles = ${Configuration.setting('Tms.Consent.autoInclude.styles')}
            tagName = 'link'
            attributes {
                rel = 'stylesheet'
                href = Neos.Fusion:ResourceUri {
                    path = 'resource://Tms.Consent/Public/Styles/main.css'
                }
            }
        }
    }
    body {
        javascripts.tmsConsent = Neos.Fusion:Array {
            @if.enabled = ${Configuration.setting('Tms.Consent.enabled')}
            @position = 'end 99999'

            config = Tms.Consent:Klaro.Config
            config.@if.autoIncludeConfig = ${Configuration.setting('Tms.Consent.autoInclude.config')}

            noBundle = Neos.Fusion:Tag {
                @if.autoIncludeScripts = ${Configuration.setting('Tms.Consent.autoInclude.scripts')}
                tagName = 'script'
                attributes {
                    defer = true
                    src = Neos.Fusion:ResourceUri {
                        path = 'resource://Tms.Consent/Public/JavaScript/NoConsentPlaceholder.js'
                    }
                }
            }

            bundle = Neos.Fusion:Tag {
                @if.autoIncludeScripts = ${Configuration.setting('Tms.Consent.autoInclude.scripts')}
                @position = 'after noBundle'
                tagName = 'script'
                attributes {
                    defer = true
                    src = Neos.Fusion:ResourceUri {
                        path = 'resource://Tms.Consent/Public/JavaScript/main.bundle.js'
                    }
                }
            }
        }

        tmsConsentSettingsButton = Tms.Consent:Component.Atom.ConsentSettingsButton
        @process.appendTmsConsentSettingsButton = ${value + this.tmsConsentSettingsButton}
        @process.appendTmsConsentSettingsButton.@if.enabled = ${Configuration.setting('Tms.Consent.enableStickyConsentSettingsButton')}
    }
}
